plugins {
    // fabric-loom / dev.architectury.loom version is declared in settings.gradle
    id 'net.neoforged.moddev' version "${moddevgradle_version}" apply false
    id 'net.neoforged.moddev.legacyforge' version "${moddevgradle_version}" apply false
    id 'net.minecraftforge.gradle' version "${forgegradle_version}" apply false
}

subprojects {
    apply plugin: 'java'

    java {
        // JDK 8 does not support --release flag; use project JDK with options.release instead
        if ((java_version as int) >= 9) {
            toolchain {
                languageVersion = JavaLanguageVersion.of(java_version as int)
            }
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.release = java_version as int
    }

    repositories {
        maven { url = 'https://libraries.minecraft.net/' }
        mavenCentral()
    }

    afterEvaluate {
        if (!project.name.endsWith('-base') && project.name != 'common') {
            base {
                archivesName = "${mod_id}-${mod_version}+${minecraft_version}-${project.name}"
            }
        }
    }
}

apply from: 'gradle/multi-version-tasks.gradle'
