plugins {
    id 'fabric-loom'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(java_version_1201 as int)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.release = java_version_1201 as int
}

configurations {
    commonJava {
        canBeResolved = true
    }
    commonResources {
        canBeResolved = true
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version_1201}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_api_version_1201}"

    compileOnly project(':common-1.20.1')
    compileOnly project(':fabric-base')

    commonJava project(path: ':common-1.20.1', configuration: 'commonJava')
    commonResources project(path: ':common-1.20.1', configuration: 'commonResources')
}

sourceSets.main {
    java {
        srcDir rootProject.file('fabric-base/src/main/java')
    }
    resources {
        srcDir rootProject.file('common-1.20.1/src/main/resources')
    }
}

tasks.named('compileJava') {
    dependsOn configurations.commonJava
    source configurations.commonJava
}

tasks.named('processResources') {
    filesMatching('fabric.mod.json') {
        expand(
            version: mod_version,
            minecraft_version: minecraft_version_1201,
            fabric_loader_version: fabric_loader_version,
        )
    }
}
