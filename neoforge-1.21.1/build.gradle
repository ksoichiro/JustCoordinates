plugins {
    id 'net.neoforged.moddev'
}

neoForge {
    version = neoforge_version

    runs {
        client {
            client()
        }
    }

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

configurations {
    commonJava {
        canBeResolved = true
    }
    commonResources {
        canBeResolved = true
    }
}

dependencies {
    compileOnly project(':common-1.21.1')
    compileOnly project(':neoforge-base')

    commonJava project(path: ':common-1.21.1', configuration: 'commonJava')
    commonResources project(path: ':common-1.21.1', configuration: 'commonResources')
}

sourceSets.main {
    java {
        srcDir rootProject.file('neoforge-base/src/main/java')
    }
}

tasks.named('compileJava') {
    dependsOn configurations.commonJava
    source configurations.commonJava
}

tasks.named('processResources') {
    dependsOn configurations.commonResources
    from configurations.commonResources

    filesMatching('META-INF/neoforge.mods.toml') {
        expand(
            version: mod_version,
            minecraft_version: minecraft_version,
            neoforge_version: neoforge_version,
        )
    }
}
